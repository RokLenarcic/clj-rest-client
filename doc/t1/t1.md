# Tutorial 1 - First Request

Hello and welcome to `clj-rest-client` tutorials.

We're building a partial GitHub REST client.

Let us start by importing the correct libs to namespace:

```clojure
(ns example.core
  (:require [clj-rest-client.core :refer [defrest]]
            [clj-http.client :as client]))
```

Now let's make a client that hits a single GitHub API endpoint.

```clojure
(defrest
  {"https://api.github.com"
   {"organizations" {GET (list-organizations [])}}})
```

This defines a function named `list-organizations` without any parameters.

Calling the function returns a clj-http map:

```clojure
{:clj-rest-client.core/args [],
 :clj-rest-client.core/name example.core/list-organizations,
 :url "https://api.github.com/organizations",
 :query-params {},
 :request-method :get,
 :as :json}
```

This a standard clj-http map with two extra (namespaced) keys that are there for you to use in logging.

All we have to do is push this map into `clj-http.client/request`:

```clojure
(client/request (list-organizations))
```

And voila, you get back a list of organizations. Not very impressive compared to just making the request manually.

But more features will be demonstrated.

### Adding a query parameter

Organizations endpoint support an optional `since` parameter, described as: `The integer ID of the last Organization that you've seen.`.

Let's add support for that:

We'll need the spec namespace imported as `s`.

```clojure
(ns example.core
  (:require [clj-rest-client.core :refer [defrest]]
            [clj-http.client :as client]
            [clojure.spec.alpha :as s]))
```

Then change the definition to:

```clojure
(defrest
  {"https://api.github.com"
   {"organizations" {GET (list-organizations [since (s/nilable pos-int?)])}}})
```

The argument vector is a vector of pairs: **parameter + spec**. The specs are checked. 

```clojure
(list-organizations nil) -> works
(list-organizations 1) -> works
(list-organizations "A") -> error
``` 

### Github API sidebar

Github limits the number of requests you can make. You can see the response headers:

```clojure
           "X-RateLimit-Limit" "60",
           "X-RateLimit-Remaining" "59",
           "X-RateLimit-Reset" "1549410837",
``` 

Obviously, sixty requests per hour is quite limiting. We can expand that limit to 4000 by authenticating.

Authentication options are listed here: https://developer.github.com/v3/#authentication

Personally, I'd generate a personal access token and use it as a password. Let us add that to our API definition.

```clojure
(defrest
  {"https://api.github.com"
   {"organizations" {GET (list-organizations [since (s/nilable pos-int?)] {:basic-auth ["user" "personal-token"]})}}})
```

Replace `user` with your username and `personal-token` with your personal access token.

Now you should see your rate limit to be about 4000.

### API definition structure

Above you can see the basic API definition structure:

It's a nested map of URL fragments, which get joined by `/` automatically.

The endpoint is defined by map key that is a symbol or keyword designating HTTP method and a function stub.

```clojure
{GET (list-organizations [since (s/nilable pos-int?)] {:basic-auth ["user" "personal-token"]})}
```
Symbol `GET` can be specified as `GET`, `get`, `:get`, `:GET`.

The function stub is composed of:

- function symbol (or keyword) `list-organizations`
- an optional clojure.spec for the function (see explanation later)
- parameter list, a vector of symbol + spec pairs
- an optional `extras` argument, which is a map of clj-http parameters that are merged on top

### Default method

In the case when the endpoint is the only entry in the map, you can skip specifying method and it defaults
to `GET`.

```clojure
(defrest
  {"https://api.github.com"
   {"organizations" (list-organizations [since (s/nilable pos-int?)] {:basic-auth ["user" "personal-token"]})}})
```

You can change the default method with an additional parameter to `defrest`. This is useful for APIs where post requests dominate.

```clojure
(defrest
  {"https://api.github.com"
   {"organizations" {GET (list-organizations [since (s/nilable pos-int?)] {:basic-auth ["user" "personal-token"]})}}}
  :default-method :post)
```

### Merging sidebar

**All merges in this framework are deep merges using `meta-merge` library. See https://github.com/weavejester/meta-merge for specifics.**

### [CODE: End State](t1.clj)
