# Tutorial 2 - Parameter types

Hello and welcome to tutorial about parameter types.

In the previous tutorial we did a simple request with a query parameter.

**By default all parameters are query parameters.**

## Path parameters

A very common type is path parameters. Let's add a call to the endpoint that fetches user's organizations.

```clojure
(defrest
  {"https://api.github.com"
   {"users/{username}/orgs" {GET (list-user-organizations [username string?] {:basic-auth ["user" "personal-token"]})}
    "organizations" {GET (list-organizations [since [:maybe pos-int?]] {:basic-auth ["user" "personal-token"]})}}})
```

Now you can try:

```clojure
(client/request (list-user-organizations "ikitommi"))
```

The path parameter is defined by enclosing part of the path in curly braces. You need to add the path parameter's name + schema to the parameter list of
all subpaths. If you have lots of endpoints under some path parameter then it can become tiresome to be adding `username string?` to all the parameter vectors.

To make the definition more DRY you can supply path parameter's schema in-line like so:

```clojure
(defrest
  {"https://api.github.com"
   {["users/{username}/orgs" string?] {GET (list-user-organizations [] {:basic-auth ["user" "personal-token"]})}
    "organizations" {GET (list-organizations [since [:maybe pos-int?]] {:basic-auth ["user" "personal-token"]})}}})
```

This notation adds parameter `username` to all endpoints defined under this subpath. If you have multiple path parameters just enumerate all schemas:

```clojure
["users/{x}/{y}/{z}" string? int? int?]
``` 

Of course you can split the definition:

```clojure
{"users" {"{x}" {"{y}" {"{z}"}}}}
```

## Defaults

Before we move on, we notice that our `extras` map with `:basic-auth` has now repeated twice. `defrest` supports `:defaults` option
for things we want to add to all request maps.

Thus let us change our API definition to:

```clojure
(defrest
  {"https://api.github.com"
   {["users/{username}/orgs" string?] {GET (list-user-organizations [])}
    "organizations" {GET (list-organizations [since [:maybe pos-int?]])}}}
  :defaults {:basic-auth ["user" "personal-token"]})
```

## Other parameters

Other parameter types are achieved by annotating the parameter symbol.

#### Form parameters

Unfortunately I cannot demonstrate form parameters on GitHub API since they don't
use them.

To add a form parameter add `:form` meta to a parameter symbol:

```clojure
{"example" {GET (example [^:form form-param any?])}}
```

You can also add `:form-map` meta to a parameter symbol. In that case, the parameter should be a map,
and that map is added as-is to form parameters.

#### Body parameter

Body parameter is added by adding `:body` meta to a parameter symbol. In most REST APIs the body should be a JSON string.

There is a `defrest` macro option that governs how body params are treated.

```clojure
  `:jsonify-bodies` set to `:always`, `:smart`, `:never`. Body params will be ran through serializer if set to `:always`. Option `:smart` will not
  run string or bytes bodies through JSON serializer. Defaults to :smart.
```

In our case, the default `:smart` options work fine.

Let us add endpoints for dealing with assignees to issues. 

```clojure
(defrest
  {"https://api.github.com"
   {["users/{username}/orgs" string?] {GET (list-user-organizations [])}
    "organizations" {GET (list-organizations [since [:maybe pos-int?]])}
    ["repos/{owner}/{repo}" string? string?]
      {"assignees" {GET (list-assignees [])}
       "issues" {["{issue_no}" pos-int?] {GET (get-issue [])
                                          "assignees" {POST (add-issue-assignees [^:body assignees any?])
                                                       DELETE (remove-issue-assignees [^:body assignees any?])}}}}}}
  :defaults {:basic-auth ["user" "personal-token"]})
```

Notice the `^:body` params with `any?` schema. But what should the object look like? Look into GitHub documentation.

Here's an example call:

```clojure
(client/request (add-issue-assignees "RokLenarcic" "AhoCorasick" 45 {:assignees ["RokLenarcic"]}))
```

We can help the user here by replacing that `any?` schema with a more detailed map schema.

The amount of APIs that use shallow JSON objects as parameters is high enough that there's a shorthand for that,
and that's the next parameter type.

#### Key parameter

Instead of using `:body` parameter you can use `:key` parameters. You cannot use both. Key parameters are
collected into a JSON object and sent as a body JSON string. Here we can change our definition:

```clojure
(defrest
  {"https://api.github.com"
   {["users/{username}/orgs" string?] {GET (list-user-organizations [])}
    "organizations" {GET (list-organizations [since [:maybe pos-int?]])}
    ["repos/{owner}/{repo}" string? string?]
      {"assignees" {GET (list-assignees [])}
       "issues" {["{issue_no}" pos-int?] {GET (get-issue [])
                                          "assignees" {POST (add-issue-assignees [^:key assignees [:sequential string?]])
                                                       DELETE (remove-issue-assignees [^:key assignees [:sequential string?]])}}}}}}
  :defaults {:basic-auth ["user" "personal-token"]})
```

Now the call becomes:

```clojure
(client/request (add-issue-assignees "RokLenarcic" "AhoCorasick" 45 ["RokLenarcic"]))
```

This is easier for the caller, because they don't have to know the keys for the JSON body by heart.

#### Hidden parameter

The last parameter type is the hidden parameter, annotated with `:+`. 
This is used when you want a parameter to use in the extras map,
yet you don't want the parameter to be a query parameter.

Extras map expression has access to all parameters.

Here's a demonstration:

```clojure
(defrest
 {"https://some.api.com"
  {"user" {GET (get-user [username string? ^:+ password string?] {:basic-auth [username password]})}}})
```

This will define an endpoint that has two parameters, but `username` is the only query parameter, 
password is only used in the extras map expression.

So now that we have parameters down we move on to make the whole thing more injectable.

### **[CODE: End state](t2.clj)**
